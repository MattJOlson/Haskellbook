# Folds

Okay, so foldr first, whatever

Idea is that map operates on "scalars" and foldr operates on cons cells,
which is kind of neat? Might be a more interesting point when we get to
Foldable, rather than just lists.

Nascent pattern:

```
firstRest :: [a] -> a
firstRest [] = k
firstRest (a:as) = a `op` (firstRest as)
```

Yeah foldable, `firstRest = foldr op k`

As an aside, I'm getting really into pointfree style aren't I.

## Strictness

`foldr (+) 0 [1, 2, 3]` only evaluates fully because + is strict in both
of its args. If it short-circuted (just returned the first arg, say), it
wouldn't continue evaluating all the way to the last cons cell (3, []),
because laziness. This is an interesting advantage of Haskell over
strict langs, maybe one that rarely comes into play too.

"One initially non-obvious aspect of folding is that it happens in two
stages, traversal and folding. Traversal is the stage in which the fold
recurses over the spine. Folding refers to the evaluation or reduction
of the folding function applied to the values. All folds recurse over
the spine in the same direction; the dierence between left folds and
right folds is in the association, or parenthesization, of the folding
function and, thus, which direction the folding or reduction proceeds."

"Given this two-stage process and non-strict evaluation, if5S doesnt
evaluate its second argument (rest of the fold), no more of the spine
will be forced. One of the consequences of this is that foldr can avoid
evaluating not just some or all of the values in the list, but some or
all of the listjs spine as well! For this reason, foldr can be used with
lists that are potentially infinite."

Something about the `firstRest` pattern matching: because we have to
pattern-match the first cons cell against [], we're forced to evaluate
the first element of the spine of the list/Foldable. So even a no-op
like `unit` against `undefined` will hit bottom.

"Scans are like folds, but return intermediate results" -- and in a
different order for foldl and foldr, due to differing associativity. I
presume this'll make more sense with a nonassociative operator.

```
last (scanl f z xs) = foldl f z xs
head (scanr f z xs) = foldr f z xs
```

Actually wait, here it is for (+):

```
Prelude> scanl (+) 0 [1..5]
[0,1,3,6,10,15]
Prelude> scanr (+) 0 [1..5]
[15,14,12,9,5,0]
```

foldl puts the zero on the left-hand end, foldr on the right. This is
obvious with a little thought but not really at a glance.

```
foldl (+) 0 [1..3] => ((0 + 1) + 2) + 3
foldr (+) 0 [1..3] => 1 + (2 + (3 + 0))
```

This has _Implications_ if we're folding on a function that doesn't have
a type of `a -> a -> a`, for example `(:) :: a -> [a] -> [a]` works fine
with `[]` as its zero in foldr, but needs to be flipped in foldl.

exercise 5 in the first set, fixing `foldl ((++) . show) "" [1..5]`, has
a couple interesting solutions:

```
Prelude> foldl (flip $ (++) . show) "" [1..5]
"54321"
Prelude> foldr ((++) . show) "" [1..5]
"12345"
```

"a left fold has the successive steps of the fold as its first argument.
The next recursion of the spine isn't intermediated by the folding
function as it is in foldr, which also means recursion of the spine is
unconditional. Having a function that doesn't force evaluation of either
of its arguments won't change anything"

Yep, sadly, `foldl const 'a' (repeat 1)` goes off into the weeds
forever.

foldl is inappropriate for infinite lists, or even very long ones
because it keeps running down the spine. But muh tail recursions! (I
feel like I'm being set up for something here.)

Oh yes, on the very next page, it's foldl'. foldl' is strict, so it
forces evaluation rather than accumulating a bunch of thunks, which is
almost certainly what I want for muh tail recursions. Does mean I won't
be able to ignore _|_s in places I "know" I'll never want to evaluate,
though.

Huh, it's not in the standard prelude as far as I can tell, though. Oh,
it's in Data.List, and I guess because it's strict it also goes off in
the weeds on infinite lists.

## Differences between folds
### `foldr`
- Not tail-recursive like foldl
- alternates between folds and calls to folding function
- associates to the right
- works with infinite lists
- suitable default

### `foldl`
- tail-calls through the list, only starts producing values when it hits
  the final cons cell
- associates to the left
- use `foldl'` in real code instead
- because it needs to find that last cons cell, no bueno with infinite
  lists

## Scans
Like folds, but produces intermediate results

```
fibs :: [Integer]
fibs = 1 : scanl (+) 1 fibs

*Ch10Ex> fibs !! 1
1
*Ch10Ex> fibs !! 12
233
*Ch10Ex> fibs !! 121
14028366653498915298923761
*Ch10Ex> take 10 fibs
[1,1,2,3,5,8,13,21,34,55]
```

Okay, let's look at one of the exercises.

```
myAny :: (a -> Bool) -> [a] -> Bool
myAny f xs = foldr (||) False $ map f xs
myAny f = foldr (||) False . map f
myAny = (foldr (||) False .) . map
```

First one's pretty obvious, right? But we want to go pointfree. (.)
associates to the right, so in the second `myAny`, `(. map f)` is the
last arg to the `foldr`. Let's make sure the types line up:

`foldr (||) False` is a `[Bool] -> Bool` -- makes sense, feed it a list
and it'll poop out a Bool.
`map f` in this case has type `[a] -> [Bool]` -- sure, it'll run your
predicate `f` on all the things.
`(. map f)` has type `([Bool] -> c) -> [a] -> c`, though, and whoa
what's going on.

Okay, break it down, this was hard for haskell tweeps so let's check it
out. `(.)` has type `(b -> c) -> (a -> b) -> a -> c`, it's function
composition, `g . f`. So the LHS of `(. foo)` is going to be a thing
that can consume whatever `foo` produces. In this case, it's a thing
that can consume a `[Bool]`.

Now I get it; `. map f` is constrained to produce a list of Bools, and
the type declaration for `myAny` (or foldr, really) tells us that it has
to produce a Bool. This is where I'd probably stop if I was writing
production code.

Now let's look at that last one. Again, `(.)` associates to the right,
so again we'll look at `(. map)` and figure out how to plumb it in. Wow,
that was not what I expected:

`(. map) :: (([a] -> [b]) -> c) -> (a -> b) -> c`

What the fuck. Okay, makes sense with currying. `map` takes a function
`a -> b` and spits out a function that will transform your list of `a`s
to a list of `b`s. So we want to glue this together with `foldr (||)
False`, which is a `[Bool] -> Bool` How do I get from there to my
desired lhs type `(([a] -> [Bool]) -> bool)`? Well, `(.)` seems to be
pretty good at gluing stuff onto the lhs type. So:

`(foldr (||) False .) :: (a -> [Bool]) -> a -> Bool`

(Okay, it's on `Foldable`, but lists for now.) That says "give me a way
to make a list of Bools from a thing, and also a thing, and I'll give
you a Bool. And hey! It also looks a lot like the LHS to `(. map)` if
you let that signature's `c` resolve to `[a]`. And indeed:

`(foldr (||) False .) . map :: (a -> Bool) -> [a] -> Bool`

So that's fun and all, and probably worth noodling around with for a
better understanding of composition, but waaaaaay too cute to check into
a repository.
